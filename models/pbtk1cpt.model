# Description ####
# One-compartment model with first-order absorption rate and 
# linear elimination (Based on R httk package)  
#
# version 1 (This version is use to test forward simple pk)
#
# Units: 
# - time in hours
# - volumes in liters
#
# Nan-Hung Hsieh - Dec 2019

# States ------------------------------------------------------------------
States  = { 
  A_rest, 
  A_elim
};  # Quantity in central compartment (mg)

# Inputs ------------------------------------------------------------------
Inputs = { Oral_input }; # Drug input (mg)


# Outputs -----------------------------------------------------------------
Outputs = { 
  Conc,
  Conc_uM
};  # Concentration in central compartment (uM)


# Parameters --------------------------------------------------------------
# Oral input modeling
Absdose;
IngDose    = 1.0; # ingested input (mg)
Fgutabs    = 1.0; #
Period     = 0.0; # period of the exposure/no exposure cycle (h)
Tlag       = 0.0; # Absorption lagtime (h)
kgutabs    = 0.1; # Intestinal absortion rate (/h)
Oral_input = PerExp (Absdose, Period, Tlag, kgutabs);

# Molecular weight (g/mole)
MW = 100;
# Distribution volumes (L)
Vdist  = 1;
# Elimination rate constants (/h)
kelim = 1;

# Statistical parameters
# ----------------------
M_Fgutabs;
M_kgutabs;
M_kelim;
M_Vdist;
Ve_Ccompartment;

Vr_Fgutabs;
Vr_kgutabs;
Vr_kelim;
Vr_Vdist;

# Initialize --------------------------------------------------------------
Initialize{ Absdose = IngDose * Fgutabs; }


# Dynamics ----------------------------------------------------------------
Dynamics { 
  dt (A_rest) = kgutabs * Oral_input - kelim * A_rest;
  dt (A_elim) = kelim * A_rest;
}


# CalcOutputs -------------------------------------------------------------
CalcOutputs { 
  Conc = A_rest  / Vdist;
  Conc_uM = Conc / MW * 1000; 
}

End.
